load("@npm//@babel/cli:index.bzl", "babel")
load("@build_bazel_rules_nodejs//:index.bzl", "pkg_npm", "copy_to_bin")
load("@build_bazel_rules_nodejs//internal/js_library:js_library.bzl", "js_library")

SRCS = glob(
    [
        "src/**/*",
        "components/**/*",
    ],
    exclude = [
        "**/*.test.*",
        "**/__snapshots__/**",
    ],
)

filegroup(
    name = "src_files",
    srcs = SRCS,
)

filegroup(
    name = "dist_files",
    srcs = glob([
        "dist/**/*",
    ]),
)

SRC_NPM_DEPS = [
    "//packages/kbn-babel-preset",
    "@npm//classnames",
    "@npm//focus-trap-react",
    "@npm//lodash",
    "@npm//prop-types",
    "@npm//react",
    "@npm//react-ace",
    "@npm//react-color",
    "@npm//tabbable",
    "@npm//uuid",
    "@npm//@babel/core",
    "@npm//@elastic/eui",
    "@npm//core-js"
]

ALL_DEPS = SRC_NPM_DEPS

babel(
    name = "src",
    data = [
        ":src_files",
    ] + SRC_NPM_DEPS,
    output_dir = True,
    args = [
        "./%s/src" % package_name(),
        "--out-dir",
        "$(@D)/src",
        "--copy-files",
        "--no-babelrc",
        "--presets=@kbn/babel-preset/webpack_preset"
    ],
)

filegroup(
    name = "target_files",
    srcs = [
        ":src_files",
        ":dist_files",
        "package.json"
    ],
)

alias(
    name = "es5",
    actual = "target_files"
)

pkg_npm(
    name = "target",
    deps = [
        ":target_files",
    ],
)

js_library(
    name = "kbn-ui-framework",
    srcs = [
        "package.json",
    ],
    deps = [
        ":target",
    ] + ALL_DEPS,
    package_name = "@kbn/ui-framework",
    visibility = ["//visibility:public"],
)

pkg_npm(
    name = "npm_module",
    deps = [
        ":kbn-ui-framework",
    ],
    visibility = ["//visibility:public"],
)
