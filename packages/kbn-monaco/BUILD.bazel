load("@npm//@bazel/typescript:index.bzl", "ts_project")
load("@npm//@babel/cli:index.bzl", "babel")
load("@npm//webpack-cli:index.bzl", webpack = "webpack_cli")
load("@build_bazel_rules_nodejs//:index.bzl", "pkg_npm")
load("@build_bazel_rules_nodejs//internal/js_library:js_library.bzl", "js_library")

SRCS = glob(
    [
        "src/**/*",
    ],
    exclude = [
        "**/README.md",
    ],
)

filegroup(
    name = "src",
    srcs = SRCS,
)

exports_files(["tsconfig.json"], visibility = ["//visibility:public"])

SRC_NPM_DEPS = [
    "//packages/kbn-babel-preset",
    "//packages/kbn-dev-utils",
    "@npm//babel-loader",
    "@npm//css-loader",
    "@npm//del",
    "@npm//monaco-editor",
    "@npm//raw-loader",
    "@npm//regenerator-runtime",
    "@npm//supports-color",
    "@npm//typescript",
    "@npm//webpack",
]

TSC_TYPES_DEPS = [
    "@npm//@types/jest",
    "@npm//@types/node",
]

ALL_DEPS = SRC_NPM_DEPS + TSC_TYPES_DEPS

webpack(
    name = "webpack_files",
    data = SRC_NPM_DEPS + [
        ":src",
        ":webpack.config.js",
    ],
    outs = ["public/xjson.editor.worker.js"],
    args = [
        "--config",
        "$(location webpack.config.js)",
        "--env.prod",
        "-o",
        "$(@D)/xjson.editor.worker.js",
        "--display=minimal"
    ],
)

ts_project(
    name = "tsc_files",
    declaration = True,
    source_map = True,
    tsconfig = "//packages/kbn-monaco:tsconfig.json",
    srcs = SRCS,
    args = [],
    deps = ALL_DEPS,
    extends = ["//:tsconfig.json"],
    root_dir = "src",
)

filegroup(
    name = "tsc_files_types",
    srcs = [":tsc_files"],
    output_group = "types",
)

filegroup(
    name = "target_files",
    srcs = [
        ":webpack_files",
        ":tsc_files",
        ":tsc_files_types",
    ],
)

alias(
    name = "es5",
    actual = "target_files"
)

# TODO: that is not a very good practise in bazel
# pkg_npm is a terminal node so if any of the files
# it contains changes, everything needs to be rebuilt,
# which make us to loose a lot of the bazel benefits.
# We should change the build of kbn_monaco down the road
# to built server, browser and types to separate folders like
# target_server, target_web, targer_types and remap them
# into the package.json.
pkg_npm(
    name = "target",
    deps = [
        ":target_files",
    ],
)

# we need ALL_DEPS here has pkg_npm is a leaf rule
# and does not propagate its deps to the following rules
js_library(
    name = "kbn-monaco",
    srcs = [
        "package.json",
    ],
    deps = [
        ":target",
    ] + ALL_DEPS,
    package_name = "@kbn/monaco",
    visibility = ["//visibility:public"],
)

pkg_npm(
    name = "npm_module",
    deps = [
        ":kbn-monaco",
    ],
    visibility = ["//visibility:public"],
)
